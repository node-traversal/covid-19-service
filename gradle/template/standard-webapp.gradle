buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "gradle.plugin.org.gretty:gretty:3.0.2"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply from: "${project.ext.gradleScriptsDir}/standard-java.gradle"

apply plugin: 'war'
apply plugin: org.akhikhl.gretty.GrettyPlugin

def dockerPort = 8080
if (project.hasProperty('webAppPort')) {
    dockerPort = project.getProperty('webAppPort')
}

def webappDebugPort = 5005
if (project.hasProperty('webappDebugPort')) {
    debugPort = project.getProperty('webappDebugPort')
}

task dockerBuild(type:Exec) {
    //on linux
    commandLine 'docker', 'build', '--build-arg', "ARTIFACT_ID=${project.name}", '--build-arg', "ARTIFACT_VERSION=${project.version}",  '-t', "${project.name}:latest", '.'
}
dockerBuild.dependsOn 'assemble'
build.dependsOn 'dockerBuild'

// should support inputs ...
task dockerRun(type:Exec) {
    //on linux
    commandLine 'docker', 'run', '-p', "$dockerPort:8080", "${project.name}"
}
dockerRun.dependsOn 'dockerBuild'
dockerRun.doFirst {
    println "Running docker on port: $dockerPort "
}

task dockerCleanContainers(type:Exec) {
    //on linux
    commandLine 'docker', 'container', 'prune', '-f'
}
clean.dependsOn 'dockerCleanContainers'

task dockerCleanImage(type:Exec) {
    //on linux
    commandLine 'docker', 'rmi', "${project.name}:latest", '-f'
}
clean.dependsOn 'dockerCleanImage'

task dockerCleanImages(type:Exec) {
    //on linux
    commandLine 'docker', 'image', 'prune', '-f'
}
clean.dependsOn 'dockerCleanImages'

gretty {
    integrationTestTask = 'integrationTest'
    httpPort = dockerPort
    debugPort = webappDebugPort
    jvmArgs = ['-Dspring.profiles.active=integration']
}

dependencies {
    implementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '3.3.6'
    implementation group: 'org.springframework', name: 'spring-web', version: '5.2.5.RELEASE'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
}